class Piece

types
	public Type = <I>| <J> | <L> | <O> | <S> | <T> | <Z>;
	public SpriteMap = map Type to Sprite;
	public Coordinate :: x: int y: int;
	public Sprite = seq of Coordinate;
	
values
	public I_Shape_Piece: Type = <I>;
	public J_Shape_Piece: Type = <J>;
	public L_Shape_Piece: Type = <L>;
	public O_Shape_Piece: Type = <O>;
	public S_Shape_Piece: Type = <S>;
	public T_Shape_Piece: Type = <T>;
	public Z_Shape_Piece: Type = <Z>;
	public defaultSprites: SpriteMap = {
		<I> |-> [mk_Coordinate(-1, -1), mk_Coordinate(0, -1), mk_Coordinate(1, -1), mk_Coordinate(2, -1)],
		<J> |-> [mk_Coordinate(0, 1), mk_Coordinate(0, -1), mk_Coordinate(0, 0), mk_Coordinate(-1, 1)],
		<L> |-> [mk_Coordinate(0, 1), mk_Coordinate(0, -1), mk_Coordinate(0, 0), mk_Coordinate(1, 1)],
		<S> |-> [mk_Coordinate(1, -1), mk_Coordinate(0, -1), mk_Coordinate(0, 0), mk_Coordinate(-1, 0)],
		<Z> |-> [mk_Coordinate(-1, -1), mk_Coordinate(0, -1), mk_Coordinate(0, 0), mk_Coordinate(1, 0)],
		<T> |-> [mk_Coordinate(0, 0), mk_Coordinate(0, -1), mk_Coordinate(1, 0), mk_Coordinate(-1, 0)],
		<O> |-> [mk_Coordinate(0, 0), mk_Coordinate(1, 1), mk_Coordinate(1, 0), mk_Coordinate(0, 1)]
	};

instance variables
	protected translation: Coordinate;
	protected originalTiles: Sprite;
	protected sprite: Sprite;
	protected symbol: char;
	
operations
	public Piece: int * int * Type * char ==> Piece
	Piece(x,y,t,s) == (
		symbol := s;
		originalTiles := defaultSprites(t);
		sprite := defaultSprites(t);
		translation := mk_Coordinate(0, 0);
		move(x,y);
	);
	
	public static createNewPiece: nat1 * nat1 * Type * char ==> Piece
	createNewPiece(x, y, type, s) == (
		if type <> O_Shape_Piece and type <> I_Shape_Piece then (
			return new PivotPiece(x, y, type, s); 
		) else (
			return new Piece(x, y, type, s);
		)
	);
	
	public getTiles: () ==> seq of Coordinate
	getTiles () == (
		return sprite;
	);
	
	public move: int * int ==> ()
	move(x,y) == (
		translation.x := translation.x + x;
		translation.y := translation.y + y;
		for inc = 1 to len sprite do (
			sprite(inc).x := sprite(inc).x + x;
			sprite(inc).y := sprite(inc).y + y;
		);
	);
	
	public getSymbol : () ==> char
	getSymbol () == return symbol;
	
	public rotate: nat1 ==> ()
	rotate(rot) == (
		return;
	);
	
functions

end Piece